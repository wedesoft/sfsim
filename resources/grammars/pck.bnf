START        = <HEAD> (DATA | <TEXT>)*
HEAD         = MAGIC LINE*
<DATA>       = <BEGINDATA> (ASSIGNMENT | <WHITESPACE> | <NEWLINE>)*
TEXT         = <BEGINTEXT> LINE*
ASSIGNMENT   = IDENTIFIER <WHITESPACE?> OPERATOR <WHITESPACE?> VALUE
<VALUE>      = VECTOR | STRING | ( NUMBER / DECIMAL )
<OPERATOR>   = EQUALS | PLUSEQUALS
MAGIC        = #'KPL/(FK|PCK)\r?\n'
BEGINDATA    = WHITESPACE? '\\begindata' WHITESPACE? NEWLINE
BEGINTEXT    = '\\begintext' WHITESPACE? NEWLINE
LINE         = !BEGINDATA #'.*\r?\n'
NUMBER       = #'[-+]?[0-9]+'
DECIMAL      = #'[-+]?[0-9]+(\.[0-9]*([DE][+-]?[0-9]+)?|[DE][+-]?[0-9]+)'
STRING       = <'\''> #'[^\']*' <'\''>
EQUALS       = <'='>
PLUSEQUALS   = <'+='>
VECTOR       = <'('> (<WHITESPACE> | <NEWLINE> | VALUE)* <')'>
<IDENTIFIER> = #'[a-zA-Z_0-9]+'
WHITESPACE   = #'[,\ \t]+'
NEWLINE      = #'\r?\n'
